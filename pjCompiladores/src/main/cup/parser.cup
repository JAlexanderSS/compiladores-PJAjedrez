package org.compiladores;
import org.compiladores.semantica.Semantica;
import org.compiladores.MyParser;
import java_cup.runtime.*;

class Parser;

terminal FILA, COLUMNA, REY, DAMA, ALFIL, CABALLO, TORRE, PEON, CAPTURA, PROMOCION, CAP, ENROQUELARGO, ENROQUECORTO, JAQUEMATE, JAQUE, COMENTARIO, ESPACIO, TAB;

non terminal captura, cdp, cdpap, pdp, dpc, dpf, dpa, cca, movimiento, jque, jquemate, movimientofinal, comentario, entrada;
non terminal desplazamiento;
non terminal pieza;
non terminal casilla;
non terminal desplazamientopeon;
non terminal movimientoB;
non terminal movimientoN;
non terminal jugada;

start with entrada;

entrada ::=
    jugada TAB entrada
    | jugada
    | jquemate comentario {: RESULT = Semantica.prueba(); Semantica.contadorJugadas();:}
    | movimientoB TAB comentario {: RESULT = Semantica.prueba(); Semantica.contadorJugadas();:}
    ;

jugada ::=
    movimientoB ESPACIO movimientoN {: RESULT = Semantica.prueba(); Semantica.contadorJugadas();:}
 ;
movimientoB ::=
    movimientofinal {: Semantica.tipoJugadorGet("Blancas");Semantica.valoresObtenidos();:}
    ;

movimientoN ::=
    movimientofinal {: Semantica.tipoJugadorGet("Negras");Semantica.valoresObtenidos();:}
    ;

movimientofinal ::=
    movimiento comentario
    | jque comentario
    ;

movimiento ::=
    desplazamiento
    | captura
    | cdp
    | cdpap
    | pdp
    | dpc
    | dpf
    | dpa
    | ENROQUECORTO
    | ENROQUELARGO
    ;

jque ::=
    movimiento JAQUE
    ;

jquemate ::=
    movimiento JAQUEMATE
    ;

pieza ::=
    REY {: Semantica.tipoPieza("Rey");:}
    | DAMA {: Semantica.tipoPieza("Dama");:}
    | ALFIL {: Semantica.tipoPieza("Alfil");:}
    | CABALLO {: Semantica.tipoPieza("Caballo");:}
    | TORRE {: Semantica.tipoPieza("Torre");:}
    | PEON {: Semantica.tipoPieza("Peon");:}
    ;

casilla ::=
    COLUMNA:col FILA:fil {: Semantica.casilla((String) col, (String) fil); :}
    ;

desplazamiento ::=
    pieza casilla {: Semantica.tipoMovimiento(0);:}
    | desplazamientopeon {: Semantica.tipoMovimiento(0);:}
    ;

desplazamientopeon ::=
    casilla  {: Semantica.tipoPieza("Peon");:}
    ;

captura ::=
    pieza CAPTURA casilla {: Semantica.tipoMovimiento(2);:}
    ;

cdp ::=
    COLUMNA:precedenciaX CAPTURA desplazamientopeon {: Semantica.tipoMovimiento(2); Semantica.calculoPrecedenciaX((String) precedenciaX);:}
    ;

cdpap ::=
    COLUMNA CAPTURA casilla PROMOCION CAP
    ;

pdp ::=
    desplazamiento PROMOCION pieza {: Semantica.tipoMovimiento(4);:}
    |
    COLUMNA CAPTURA casilla PROMOCION pieza {: Semantica.tipoMovimiento(4);:}
    ;

cca ::=
    CAPTURA
    |
    ;

dpc ::=
    pieza COLUMNA cca casilla
    ;

dpf ::=
    pieza FILA cca casilla
    ;

dpa ::=
    pieza casilla cca casilla
    ;

comentario ::=
    COMENTARIO
    |
    ;